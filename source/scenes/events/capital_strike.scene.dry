title: 자본 파업
subtitle: 독일의 산업이 멈춰 서고 있다...
view-if: (capital_strike_progress >= 10 or budget <= -5) and spd_in_government
new-page: true
max-visits: 1
on-arrival: unemployed += 5; workers_spd*= 0.7; new_middle_spd -= 15; old_middle_spd -= 15; unemployed_spd*=0.7; rural_spd -= 15; pro_republic -= 15; capital_strike_seen = 1; z_relation -= 10; dvp_relation -= 10; workers_nsdap += 8; unemployed_nsdap += 10; new_middle_nsdap += 15; old_middle_nsdap += 15; rural_nsdap += 15; economic_growth -= 4;
tags: event

= 자본 파업

우리의 정책은 자본가들의 분노를 샀습니다. 투자는 끊기고 공장은 가동을 멈췄으며, 노동자들은 일터에서 쫓겨나고 있습니다. 한편, 부르주아 언론은 우리와 우리의 경제 정책을 향해 끊임없이 악의적인 비난을 퍼붓고 있습니다.

우리는 무엇을 할 수 있을까요?

- @seize
- @capital_controls
- @propaganda
- @nothing

@seize
title: 노동자들에게 공장을 접수할 권한을 부여한다!
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : 당내 다수가 이 급진적인 조치에 반대합니다. ?] [? if judicial_reform < 3 : 사법부가 이 조치를 가로막을 것입니다. ?] [? if rb_militancy < 0.25 : 우리의 준군사조직이 더 강했다면 가능했을지도 모릅니다. ?] [? if works_councils < 1 : 먼저 노동자 평의회의 지지를 확보해야 합니다. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; coup_progress += 6; unemployed -= 2; unemployed -= 1 if works_councils >= 3; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; dvp_relation -= 4; workers_nsdap -= 8*(1-dissent); unemployed_nsdap -= 8*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; factory_takeovers += 1; economic_growth += 2;

자본가들이 우리를 공격한다면, 우리도 되갚아주어야 합니다. 우리는 노동자들에게 자본가들이 멈춰 세운 공장을 접수하여 완전히 통제할 권한을 부여했습니다. 물론 파시스트들은 우리를 도둑, 약탈자라고 부릅니다.


@capital_controls
title: 피해를 줄이기 위해 자본 통제를 시행한다.
choose-if: judicial_reform >= 2
unavailable-subtitle: 사법부가 결코 허용하지 않을 것입니다.
on-arrival: unemployed -= 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); economic_growth += 2;

자본 통제는 '자본 파업'이 초래한 최악의 경제적 충격 일부를 완화할 수 있을 것입니다.



@propaganda
title: 위기의 책임을 자본가에게 돌리는 선전전을 펼친다.
subtitle: 자금 -1
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 : 우리의 우리의 미디어를 충분히 구축하지 않았습니다 ?] [? if resources < 1: 자금이 부족합니다. ?]
on-arrival: workers_spd += 7*(1-dissent); new_middle_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); pro_republic += 5*(1-dissent); resources -= 1

우리가 가진 모든 언론 채널을 동원하여 경제 위기의 책임을 자본가에게 돌리는 역선전 공세를 펼쳤습니다. 우리가 구축해온 언론 인프라 덕분에, 대중의 비난 화살을 우리에게서 자본가들에게로 돌리는 데 어느 정도 성공했습니다.


@nothing
title: 우리가 할 수 있는 일은 없다.

헌법과 법률의 테두리 안에서는 우리가 할 수 있는 일이 없습니다.
