title: 오스트리아 관세 동맹
subtitle: 외무부, 오스트리아와의 관계 강화를 모색하다...
new-page: true
on-arrival: customs_union_seen = 1
tags: event
view-if: year = 1931 and month >= 3 and foreign_minister_party != "SPD" and foreign_minister_party != "KPD" and customs_union == 0 and not eu
max-visits: 1

# TODO: this should happen regardless of the current state of the government...

= 오스트리아 관세 동맹

[? if spd_in_government: [+ foreign_minister_party +] 외무장관이 오스트리아와의 관세 동맹을 제안했습니다. 우리는 이 협정을 받아들이거나 거부할 수 있습니다. ?]
[? if not spd_in_government: [+ chancellor +] 총리 정부가 오스트리아와의 관세 동맹을 제안했습니다. 우리의 선택과 무관하게 강행될 가능성이 높습니다. ?]


민족주의 우파는 두 독일어권 국가의 합병을 최종 목표로 삼고 오스트리아와의 관계 강화를 오랫동안 갈망해왔습니다... 또한 많은 이들은 오스트리아와의 통합이 현재의 어려운 경제 상황을 개선해 줄 것이라 믿고 있습니다.

하지만 연합국, 특히 프랑스가 반대할 것이 분명합니다. 이 협정은 베르사유 조약을 위반하는 것이며, 경제 제재로 이어져 우리 경제에 더 큰 타격을 줄 수 있습니다.

- @let_it_happen: 그대로 추진하게 둔다.
- @end_coalition: 이 협정을 중단시킨다!
- @end_toleration: 협정 중단을 시도한다!

@let_it_happen
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

관세 동맹이 체결되었습니다. 즉각 프랑스를 비롯한 주변국들이 항의를 제기했습니다. 이들의 장기적인 대응은 아직 지켜봐야 할 것입니다.

@end_coalition
view-if: spd_in_government = 1
on-arrival: coalition_dissent += 1; dvp_relation -= 5; z_relation -= 4; z_relation -= 3 if chancellor_party == "Z"; dvp_relation -= 3 if chancellor_party == "Z"

우리는 연정 파트너들에게 연합국의 대응이 불러올 심각한 결과를 설득하려 시도했습니다. 독일 인민당(DVP)과 중앙당이 불만을 표했지만, 제안은 철회되었습니다.


@end_toleration
view-if: spd_toleration = 1
subtitle: This is difficult; all of the right-of-center parties support this policy.
go-to: end_toleration_success if spd_r + kpd_r + ddp_r >= 50; end_toleration_fail if spd_r + kpd_r + ddp_r < 50

@end_toleration_success
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;
call: set_next_election_time

공산당과 [+ ddp_name +]의 지지를 얻어, 관세 동맹 체결을 성공적으로 저지했습니다. 이로 인해 [+ chancellor +] 내각은 붕괴했습니다.

새로운 총선은 [+ next_election_year +]년 [+ next_election_month : month +]월에 치러질 예정입니다.

@end_toleration_fail
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

안타깝게도, 극우 정당들이 정부와 손을 잡고 표결에서 우리를 눌렀습니다. 관세 동맹은 예정대로 체결될 것입니다.
