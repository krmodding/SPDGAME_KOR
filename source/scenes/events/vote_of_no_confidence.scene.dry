title: 우파가 현 정부에 대한 불신임 투표를 소집하다!
subtitle: 연정 파트너들을 너무 화나게 한 것 같다…
new-page: true
on-arrival: tried_supporting_kpd = 0
view-if: spd_in_government = 1 and ((in_grand_coalition = 1 or in_popular_front = 1 or in_minority_government = 1) and coalition_dissent >= 3) or (in_weimar_coalition = 1 and coalition_dissent >= 4) and spd_r < 50 and not constructive_vonc
tags: event
go-to: vonc_menu

# Options: spend relations to reduce coalition_dissent, ask for kpd to abstain
# (lmao KPD toleration arc...)

= 불신임 투표

[? if in_grand_coalition : 독일인민당(DVP) ?][? if in_weimar_coalition or in_popular_front or in_minority_government: 중앙당 ?]이 우리의 다양한 정책과 의견 차이로 인해 정부 불신임 투표를 소집했습니다. 이 안이 통과되면, 몇 달 안에 총선이 치러질 것입니다.

@vonc_menu

- @try_assuage: 연정 파트너들을 설득해 본다.
- @support_kpd: 공산당이 기권하도록 설득할 수 있을까?
- @let_it_happen: 투표가 진행되도록 내버려 둔다.

@try_assuage

어떻게 연정 파트너들을 설득할까요?

- @give_up_prussia: 프로이센 지도부를 포기한다.
- @austerity: 대규모 긴축 정책을 시행한다.
- @resources: 우리 자금을 사용해 연정 내 불만을 잠재운다.
- @support_kpd: 공산당이 기권하도록 설득할 수 있을까?
- @let_it_happen: 그만두자. 투표가 진행되도록 내버려 둔다.

@give_up_prussia
view-if: spd_prussia == 1
on-arrival: coalition_dissent = 0; spd_prussia = 0; prussian_government = "I"; reformist_dissent += 10; centrist_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_spd -= 5; rural_spd -= 5; 

좋습니다. 이제 우리는 프로이센 주정부를 통제하지 않습니다. 불신임 투표는 철회되었습니다.

@austerity
view-if: welfare >= 0 and not secularized
on-arrival: coalition_dissent = 0; workers_spd -= 10; unemployed_spd -= 10; workers_kpd += 7; workers_nsdap += 3; unemployed_kpd += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; kpd_coalition_dissent += 1; kpd_relation -= 10 if in_popular_front

좋습니다. 연정 안정을 위해 복지를 삭감하는 대규모 긴축 프로그램을 시행했습니다. 불신임 투표는 철회되었습니다.

@resources
view-if: secularized = 0
choose-if: resources >= 3
unavailable-subtitle: 자금이 충분하지 않습니다.
subtitle: 자금 -3
on-arrival: resources -= 3; coalition_dissent -= 1;

연정 파트너에게 자그을 넘겨주는 것으로 우리 정책에 대한 그들의 반발을 줄일 수 있습니다. 조악하지만 효과적인 해결책입니다.

@support_kpd
choose-if: (in_grand_coalition and communist_coalition >= 3 and kpd_relation >= 50 and kpd_r >= dvp_r) or (in_weimar_coalition and kpd_relation >= 50 and kpd_r >= z_r and communist_coalition >= 3)
view-if: tried_supporting_kpd = 0
on-arrival: tried_supporting_kpd = 1
unavailable-subtitle: [? if communist_coalition < 3 : 공산당은 의회 정치에 참여하지 않을 겁니다. ?] [? if kpd_relation < 50 : 우리와 공산당의 관계가 좋지 않습니다. ?] [? if (in_grand_coalition and kpd_r < dvp_r) or (in_weimar_coalition and kpd_r < z_r): 공산당은 의석 수가 부족합니다. ?] [? if in_popular_front : 우리는 이미 공산당과 인민전선을 구성하고 있습니다. ?]

만약 공산당이 기권한다면, 우리는 사실상 소수 정부를 유지할 수 있습니다.[? if president=="Hindenburg": 힌덴부르크 대통령의 지원이 없는 상황에서 정책을 펴기는 매우 어렵겠지만 말입니다. ?] 이렇게 되면, 우리는 공산당에 큰 빚을 지게 됩니다. 이 일은 좋든 나쁘든, 미래에 더 폭넓은 협력의 길을 열 수도 있습니다.

- @support_kpd_2: 공산당을 설득해 우리 정부를 '묵인’하게 할까?
- @let_it_happen: 그만두자. 투표가 진행되도록 내버려 둔다.

@support_kpd_2
on-arrival: communist_coalition += 1; workers_kpd += 3; kpd_relation += 5
go-to: support_kpd_weimar if in_weimar_coalition; support_kpd_grand if in_grand_coalition

# TODO: this can form an infinite loop

공산당은 소수 정부에 대한 '묵인’에는 관심이 없습니다. 그들은 연정의 일원으로 정부에 참여할 수 있을 때만 우리를 지지할 것입니다. 그리고 만약 이런 묵인 협정이 성사된다면, 우익 정당들이 우리를 지지할 리 없습니다.

@support_kpd_grand

- @election_1928.popular_front_coalition: "인민 전선" 연정을 창설할까?
- @election_1928.left_coalition
- @try_assuage: 그만두자. 다른 방법으로 이 문제를 해결해야 한다.

@support_kpd_weimar

- @election_1928.left_coalition
- @try_assuage: 그만두자. 다른 방법으로 이 문제를 해결해야 한다.


@let_it_happen
call: set_next_election_time
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann"; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener";  coalition_dissent = 0; pro_republic -= 5; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50
go-to: let_it_happen_hindenburg if president != "Braun"; let_it_happen_braun if president == "Braun"

@let_it_happen_hindenburg
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; welfare -= 2;

불신임안이 가결되어 우리 정부는 총사퇴했습니다. 부르주아 정당들로만 구성된 임시 내각의 수반으로 브뤼닝 총리가 임명되었습니다. 그는 즉시 복지 예산을 대폭 삭감하기 시작했습니다.

차기 총선은 [+ next_election_year +]년 [+ next_election_month : month +]월로 예정되었습니다.

@let_it_happen_braun
on-arrival: spd_caretaker = 1

불신임안이 가결되어 우리 정부는 총사퇴했습니다. [+ chancellor +] 총리는 임시 관리 내각의 수장으로 남아 있지만, 더 이상 새로운 정책을 입안할 힘은 없습니다.

차기 총선은 [+ next_election_year +]년 [+ next_election_month : month +]월로 예정되었습니다.
