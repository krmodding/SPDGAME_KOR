title: 긴급 예산 삭감
subtitle: [+ chancellor +] 총리가 사회복지 예산 대폭 삭감안을 제안합니다.
new-page: true
view-if: (year == 1929 or year == 1930 or year == 1931 or year == 1932) and month >= 12 and chancellor == "Brüning" and spd_in_government == 0 and (unemployed > 15 or (year == 1929 and unemployed > 10)) and welfare >= 0 and emergency_cuts_timer = 0
on-arrival: emergency_cuts_seen = 1; emergency_cuts_timer = 12
tags: event
max-visits: 4

= 긴급 예산 삭감

 대공황 위기로 인해 정부 재정은 악화된 반면 복지 지출은 급증했습니다. 이에 [+ chancellor +] 총리는 바이마르 헌법 제48조(긴급명령권)를 발동하여 복지 예산을 삭감하는 긴급 법안을 통과시켰습니다.

[? if spd_toleration : 만약 우리가 이 법안을 거부한다면, 이는 정부와의 묵인 협정 파기를 의미하며, [+ chancellor +] 총리는 제국의회를 해산하고 총선을 요구할 가능성이 높습니다. 노동자들은 당연히 이 정책에 반대하고 있지만, 이것이 경제 및 정치적 안정을 위해 불가피한 조치일 수도 있습니다. ?]

- @nothing_to_do: 우리가 할 수 있는 일은 없다.
- @vote_no
- @vote_yes

@nothing_to_do
view-if: spd_toleration == 0
on-arrival: welfare -= 1; unemployed += 2; unemployed_nsdap += 4; unemployed_kpd += 3; unemployed_spd += 2; pro_republic -= 4; budget += 1; inflation -= 2; economic_growth -= 0.5;

현재 제국의회가 해산된 상태이므로, 긴급명령에 따라 해당 정책이 통과됩니다. 나치는 이를 당원 모집의 기회로 활용하고 있으며, 우리 또한 마찬가지입니다.


@vote_no
title: 법안에 반대하고 묵인 협정을 파기한다.
view-if: spd_toleration
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; pro_republic -= 5
call: set_next_election_time

[+ chancellor +] 정부와 결별하고, 불신임안에 찬성표를 던졌습니다. 복지 예산은 지켜냈지만, 그 대가는 무엇일까요?

차기 총선은 [+ next_election_year +]년 [+ next_election_month : month +]월로 예정되었습니다.


@vote_yes
title: 묵인 협정을 유지하기 위해 법안에 찬성한다.
view-if: spd_toleration
on-arrival: welfare -= 1; unemployed += 2; left_dissent += 10; labor_dissent += 10; workers_spd *= 0.9; unemployed_spd *= 0.9; unemployed_nsdap += 5; unemployed_kpd += 5; workers_nsdap += 5; workers_kpd += 5; workers_spd -= 4 if welfare < 0; unemployed_spd -= 5 if welfare < 0; pro_republic -= 4; kpd_relation -= 8; budget += 1; inflation -= 2; economic_growth -= 0.5;

우리는 안정적인 의회 정부를 유지하고 대공황의 재정 위기에 대처하기 위해 이 정책이 불가피하다고 노동자들을 설득하려 하고 있습니다. 그럼에도, 이 정책은 나치와 공산주의자들이 세력을 불릴 절호의 기회를 제공하고 있습니다.
