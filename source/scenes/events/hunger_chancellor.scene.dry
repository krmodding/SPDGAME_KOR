title: 기아 총리
subtitle: [+ chancellor +] 총리에 대한 대중의 불만이 고조되고 있습니다.
new-page: true
view-if: year >= 1931 and month >= 2 and unemployed >= 18 and welfare < 0 and works_program == 0
on-arrival: workers_spd *= 0.9 if chancellor_party == "SPD"; workers_spd *= 0.9 if spd_in_government == 1; workers_nsdap += 5 if spd_in_government; workers_kpd += 5 if spd_in_government; unemployed_nsdap += 5 if spd_in_government
tags: event
max-visits: 1
face-image: img/Kollwitz_Hospital.jpg

= 기아 총리

대공황 국면에서 보인 냉혹한 태도 때문에, 사람들은 [+ chancellor +] 총리를 '기아 총리’라고 부르고 있습니다. [? if chancellor_party == "SPD" or spd_in_government : 이는 명백히 우리에게 매우 불리한 신호입니다. ?]

[? if spd_toleration == 1 : 우리가 현재 [+ chancellor +] 정부를 묵인하는 입장이므로, 이러한 비난은 우리에게도 악영향을 미칠 수 있습니다. ?]

- @maintain_toleration: 현 정부와의 관계를 유지한다.
- @break_toleration: 묵인 협정을 파기하고 정부 불신임안에 투표한다.

@maintain_toleration
subtitle: 민주주의를 지키기 위해 불가피하다.
view-if: spd_toleration == 1
on-arrival: workers_nsdap += 5; workers_kpd += 5; unemployed_nsdap += 5; workers_spd -= 5; pro_republic -= 5; kpd_relation -= 5

우리는 묵인 정책을 계속 유지합니다.


@break_toleration
subtitle: 이 결정은 총선으로 이어질 가능성이 높습니다.
view-if: spd_toleration == 1
on-arrival: next_election_time = time+3 if next_election_time >= time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;

우리는 [+ chancellor +] 정부와 완전히 결별하고, 정부 불신임안에 찬성표를 던졌습니다.

차기 총선은 [+ next_election_year +]년 [+ next_election_month : month +]월로 예정되었습니다.
