title: 경제 정책
new-page: true
is-card: true
tags: govt_affairs, cabinet
on-arrival: economic_policy_timer += 12; month_actions += 1; nationalize_budget = 4; nationalize_budget -= 1 if works_councils >= 2; nationalize_budget -= 1 if works_councils >= 3; nationalize_budget -= 1 if socializations + nationalization_progress >= 2; wtb_budget = 4; wtb_budget -= 1 if works_councils >= 2 or socializations >= 1 or pro_labor >= 2
view-if: spd_in_government = 1 and (economic_minister_party == "SPD" or finance_minister_party == "SPD") and economic_policy_timer <= 0 and economic_plan > 0
card-image: img/portraits/SchmidtRobert.jpg
#subtitle: Policies to address the economic crisis.

= 경제 정책

우리는 이미 경제 계획을 수립했습니다. 이제 실행에 옮겨야 합니다.

- @wtb_2
- @wtb_2_deficit
- @wtb_continuation
- @implement_wtb_no_deficit
- @implement_wtb_deficit
- @nationalize_1
- @nationalize_2
- @moderate_plan
- @moderate_plan_limited_budget
- @root: 경제 계획을 실행하지 않는다.
- @easy_discard
- @cancel_advisor_action

@wtb_2
title: WTB 계획의 실행을 계속한다.
subtitle: 예산 -3
unavailable-subtitle: 실업률이 충분히 낮아 WTB 계획을 실행할 시기는 지났습니다.
view-if: wtb_implemented > 2 and budget >= 3 
choose-if: unemployed > 8
on-arrival: works_program += 1; budget -= 3; workers_spd += 7*(1-dissent); workers += 1 if works_program <= 4; unemployed -= 3 if unemployed > 8; unemployed_spd += 7*(1-dissent); workers_kpd -= 3; workers_nsdap -= 3; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; economic_growth += 1.6; economic_growth -= 0.6 if economic_growth > 5

[? if works_program == 4 : WTB 계획 2단계를 시작했습니다. ?][? if works_program > 4 :  WTB 계획의 실행을 계속하고 있습니다. ?] 공공 사업뿐만 아니라, 이제는 우리나라 산업 재건에도 집중하고 있습니다.


@wtb_2_deficit
title: WTB 계획의 실행을 계속한다.
subtitle: 예산 -3 - 적자 재정에 돌입한다.
unavailable-subtitle: 실업률이 충분히 낮아 적자 재정을 감수할 필요가 없습니다.
view-if: wtb_implemented > 2 and budget < 3 and budget >= -3
choose-if: unemployed > 10
on-arrival: works_program += 1; budget -= 3; workers_spd += 6*(1-dissent); workers += 1 if works_program <= 4; unemployed -= 3 if unemployed > 8; unemployed_spd += 6*(1-dissent); workers_kpd -= 2; workers_nsdap -= 2; coalition_dissent += 1; capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; economic_growth += 1.6; economic_growth -= 0.6 if economic_growth > 5

지속적인 적자 재정에도 불구하고 WTB 계획의 실행을 계속하고 있습니다. 공공 사업뿐만 아니라, 이제는 우리나라 산업 재건에도 집중하고 있습니다.


@wtb_continuation
title: 결과를 검토하고, 계속해서 계획을 실행한다…
view-if: wtb_implemented >= 1 and wtb_implemented <= 2
on-arrival: budget += 1 if works_program <= 2; unemployed -= 5 if (works_program <= 2 and unemployed > 15) else unemployed -= 3; unemployed += 3 if unemployed < 5; unemployed += 3 if unemployed < 0; workers += 2 if works_program <= 2 else workers += 1; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); workers_kpd -= 3; workers_nsdap -= 5; works_program += 1; z_relation += 3; dvp_relation += 3; coalition_dissent -= 1 if coalition_dissent >= 1; wtb_implemented += 1; capital_strike_progress -= 1 if capital_strike_progress >= 3; inflation += 1.5; inflation += 1 if unemployed < 10; economic_growth += 1.2; economic_growth -= 0.5 if economic_growth > 5

우리의 공공 사업 지출은 실업 수당 및 복지 수요를 줄였고, 새로운 자금이 경제 전반에 흐르면서 발생하는 승수 효과 덕분에 세수가 증가하여 지출 일부를 회수했습니다. 우리는 새로운 세수 대부분을 이 프로그램에 계속 투자합니다.

마침내 경제가 나아지고 있습니다. 부르주아 정당들조차 이제야 희망을 보고 있습니다.

[? if in_popular_front or in_left_front: 하지만 공산당은 자본주의를 '구제’하려는 우리의 정책에 여전히 반대합니다. ?]

@implement_wtb_no_deficit
title: WTB 계획을 실행한다.
subtitle: 예산 -[+ wtb_budget +]. [? if wtb_budget == 3 : 우리의 친노동 정책 덕분에 계획 비용이 절감되었습니다. ?]
unavailable-subtitle: 실업률이 충분히 낮아 WTB 계획을 실행할 시기는 지났습니다..
view-if: budget >= wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8
on-arrival: budget -= wtb_budget; works_program += 1; unemployed -= 4; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; economic_growth += 2.8; pro_republic += 3*(1-dissent);
achievement: wirtschaftspolitik

WTB 계획의 실행을 시작했습니다. 대규모 공공 사업 투자를 통해 경제에 자금이 돌기 시작했습니다.

[? if in_popular_front or in_left_front: 공산당은 WTB 계획을 진정한 사회주의로 나아가는 길이 아닌 자본주의를 구제하는 것으로 보고 반대합니다. ?]

@implement_wtb_deficit
title: WTB 계획을 실행한다.
subtitle: 예산 -[+ wtb_budget +] - 적자 재정에 돌입한다.  [? if wtb_budget == 3 : 우리의 친노동 정책 덕분에 계획 비용이 절감되었습니다. ?]
unavailable-subtitle: 실업률이 충분히 낮아 WTB 계획을 실행할 시기는 지났습니다.
view-if: budget < wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8
on-arrival: coalition_dissent += 1; coalition_dissent += 1 if historical_mode; coalition_dissent += 1 if chancellor == "Brüning"; works_program += 1; budget -= wtb_budget; unemployed -= 4; workers += 2; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; economic_growth += 3; pro_republic += 3*(1-dissent);
achievement: wirtschaftspolitik

우리는 적자 재정의 영역에 들어섰습니다. 연정 파트너들은 혐오감을 표하지만, 공공 사업 계획은 예정대로 시행됩니다. 대규모 공공 사업 투자를 통해 경제에 자금이 돌기 시작했습니다.

[? if in_popular_front or in_left_front: 공산당은 WTB 계획을 진정한 사회주의로 나아가는 길이 아닌 자본주의를 구제하는 것으로 보고 반대합니다. ?]


####################### left economic policies (socializations)

@nationalize_1
title: 경제 변혁을 위한 좌파의 계획을 실행한다.
subtitle: 예산: -[+ nationalize_budget +].  [? if works_councils >= 3 : 노동자 평의회 덕분에 이 프로그램에 필요한 예산이 줄었습니다. ?] [? if socializations >= 2: 기존의 사회화 정책 덕분에 이 프로그램에 필요한 예산이 줄었습니다. ?]
view-if: nationalization_adopted == 1 and nationalization_progress == 0
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget


@nationalize_no_deficit
on-arrival: nationalization_progress += 1; capital_strike_progress += 2; unemployed -= 3.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; workers += 2 if nationalization_progress < 2; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 1; dvp_relation -= 4; left_dissent -= 5; new_middle_spd -= 2; old_middle_spd -= 3; economy_goal_completed += 1; inflation += 2; economic_growth += 1.7 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if nationalization_progress == 1 : 자본가들로부터 가동을 멈춘 기업들을 사들이기 시작했습니다.[? if works_councils >= 1: 그리고 강력해진 노동자 평의회와 협력하여 운영하고 있습니다. ?]. 생산을 계속함으로써 치솟는 실업을 막아낼 수 있습니다. ?]

[? if nationalization_progress == 1 : 좌파는 이것이 경제의 사회주의적 변혁을 위한 첫걸음에 불과하다고 주장합니다. 궁극적으로 경제의 상당 부분이 대중의 통제 하에 놓여야 합니다. ?]

[? if nationalization_progress > 1 : 우리는 추가로 활용되지 않는 기업들을 사들여, 해고되었던 노동자들을 고용하고 노동자 평의회와 협력하여 사회주의 방식으로 운영합니다. 이제 경제의 상당 부분이 공공의 통제 하에 있습니다. ?]

@nationalize_deficit

안타깝게도, 우리는 산업 시설 매입을 시작할 예산이 없습니다. 일부에서는 공장 매입을 위해 적자 재정을 감수하라고 촉구하고 있으며, 다른 한편에서는 (현재는 불법인) 노동자들의 공장 점거나 보상 없는 국유화를 부추기고 있습니다. 이 중 어떤 조치를 취하든, 이는 우리 당의 기존 입장과 크게 벗어나는 일이 될 것입니다.

- @nationalize_deficit_1
- @nationalize_without_compensation
- @empower_workers
- @return: 아니, 우리는 이걸 해서는 안 된다.


@nationalize_deficit_1
title: 국유화 프로그램을 위한 재정 적자를 감수한다.
choose-if: left_strength > reformist_strength + center_strength or woytinsky_advisor == 1
subtitle: [? 보이틴스키라면 우리를 재정 적자 지지로 이끌 수 있을 것이다. ?]
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: 당내에서는 아직 이 목적을 위한 재정 적자를 지지하지 않습니다. ?] [? if not woytinsky_advisor : 아마도 고문을 영입하면 이 조치를 받아들이게 만드는 데 도움이 될까요? ?]
on-arrival: nationalization_progress += 1; capital_strike_progress += 3; unemployed -= 3 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 2; dvp_relation -= 6; z_relation -= 4; new_middle_spd -= 3; old_middle_spd -= 3; economy_goal_completed += 1; inflation += 3; economic_growth += 2 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if woytinsky_advisor: 대부분의 당원들, 심지어 좌파 조차도 어떤 목적의 재정 적자든 반대하지만, 블라디미르 보이틴스키는 예외입니다. 그는 원래 좌파의 ‘변혁적’ 계획을 지지하지 않았지만, 생각을 바꿔 당과 재무부가 불황 탈출을 위해 제한적인 경우의 재정 적자는 지지하도록 설득했습니다. ?]

[? if nationalization_progress == 1 : 자본가들로부터 가동을 멈춘 기업들을 사들이기 시작했습니다. [? if works_councils >= 1: 그리고 강력해진 노동자 평의회와 협력하여 운영합니다. ?] 생산을 계속함으로써 치솟는 실업을 막아낼 수 있습니다. ?]

[? if nationalization_progress == 1 : 좌파는 이것이 경제의 사회주의적 변혁을 위한 첫걸음에 불과하다고 주장합니다. 궁극적으로 경제의 상당 부분이 대중의 통제 하에 놓여야 합니다. ?]

[? if nationalization_progress > 1 : 우리는 추가로 활용되지 않는 기업들을 사들여, 해고되었던 노동자들을 고용하고 노동자 평의회와 협력하여 사회주의 방식으로 운영합니다. 이제 경제의 상당 부분이 공공의 통제 하에 있습니다. ?]

@nationalize_without_compensation
title: 보상 없이 국유화한다.
subtitle: 가동을 멈춘 공장을 우리가 인수하지 못할 이유가 없습니다. 이는 자본가들과의 충돌을 유발할 가능성이 높습니다.
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: 당의 대다수가 이 불법에 가까운 조치를 지지하지 않습니다. ?] [? if judicial_reform < 3 : 사법부가 이 조치에 반대할 것입니다. ?]
choose-if: left_strength > reformist_strength + center_strength and judicial_reform >= 3
on-arrival: nationalization_progress += 1; capital_strike_progress += 7; coup_progress += 3; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3 and unemployed >= 3; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 6*(1-dissent); coalition_dissent += 2; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 8; economy_goal_completed += 1; kpd_relation += 5*(1-dissent); inflation += 4; economic_growth += 1 if economic_growth < 4;
achievement: wirtschaftspolitik

자본가들은 예상대로 보상 없는 국유화 정책에 대해 무장 봉기라도 할 기세입니다. 그들은 이를 불법적인 몰수라고 부르며, 사실 틀린말은 아닙니다. 그럼에도, 공장을 놀려두어 실패한 것은 바로 그들이므로 우리는 급진적인 조치를 취해야만 합니다.

[? if nationalization_progress == 1 : 우리는 자본가들로부터 가동을 멈춘 기업들을 인수하기 시작했습니다.[? if works_councils >= 1: 그리고 권한을 부여받은 노동자 평의회와 협력하여 운영합니다. ?] 생산을 계속함으로써 치솟는 실업을 막아낼 수 있습니다. ?]

[? if nationalization_progress > 1 : 우리는 추가로 활용되지 않는 기업들을 인수하여, 과거에 해고되었던 노동자들을 고용하고 노동자 평의회와 협력하여 사회주의 방식으로 운영합니다. 이제 경제의 상당 부분이 공공의 통제 하에 있습니다. ?]

@empower_workers
title: 노동자들의 공장 점거에 힘을 실어준다.
subtitle: 우리는 공장 점거를 합법화하여 공장을 노동조합에 넘겨주겠다고 약속할 수 있습니다. 이는 매우 대립적인 조치로, 우익 무장세력과 자본가들의 반발을 불러일으킬 가능성이 높습니다.
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : 당의 대다수가 이 급진적인 조치를 지지하지 않습니다. ?] [? if judicial_reform < 3 : 사법부가 이 조치에 반대할 것입니다. ?] [? if rb_militancy < 0.25 : 우리 준군사조직의 힘을 키웠더라면 아마 이 일을 할 수 있었을 것입니다. ?] [? if works_councils < 1 : 우선 노동자 평의회의 지지가 필요합니다. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; capital_strike_progress += 6; coup_progress += 5; unemployed -= 3 if unemployed >= 6; unemployed -= 1.5 if works_councils >= 3 and unemployed > 4; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; workers_nsdap -= 5*(1-dissent); unemployed_nsdap -= 5*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 10; factory_takeovers += 1; economy_goal_completed += 1; kpd_relation += 5*(1-dissent); inflation += 3; economic_growth += 1 if economic_growth < 3;
achievement: wirtschaftspolitik

자본가들은 예상대로 공장 점거 합법화 정책에 대해 무장 봉기라도 할 기세입니다. 그들은 이를 불법적인 몰수라고 부르며, 사실 틀린말은 아닙니다. 그럼에도, 그들이 실패했음은 분명하므로 우리는 급진적인 조치를 취해야만 합니다.

 파시스트들은 점거된 공장 주변에서 우리를 도둑과 약탈자라 부르며 점점 더 폭력적인 행동을 보이고 있습니다. 하지만 파시스트를 지지하던 노동계급 중 일부, 적어도 계급 연대 의식을 가진 이들은 떨어져 나갔습니다.


@nationalize_2
title: 경제 변혁을 위한 좌파의 계획을 계속한다.
subtitle: 예산 -[+ nationalize_budget +]. [? if works_councils >= 3 : 노동자 평의회 덕분에 이 프로그램에 필요한 예산이 줄었습니다. ?] [? if socializations + nationalization_progress >= 2: 기존의 사회화 정책 덕분에 이 프로그램에 필요한 예산이 줄었습니다. ?]
view-if: nationalization_adopted == 1 and nationalization_progress >= 1
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget

#######################################
# Moderate economic plan

@moderate_plan
title: 개혁파의 일자리 창출 계획을 실행한다.
subtitle: 예산 -2.
unavailable-subtitle: 예산 -2. [? if budget < 2 : 필요한 예산이 없습니다. ?] [? if unemployed < 8 : 실업률이 충분히 낮아 일자리 창출이 필요하지 않습니다. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 2 and unemployed >= 8
on-arrival: budget -= 2; works_program += 1; unemployed -= 3 if unemployed >= 8; inflation += 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 4*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; economic_growth += 1.6; pro_republic += 3*(1-dissent);
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : 일자리 창출 계획을 이행하기 시작했습니다! 독일 전역에 공공사업 프로젝트를 건설하기 위한 자금이 투입되어 실업률을 낮추고 있습니다. ?]

[? if moderate_plan_progress > 1 : 우리는 계속해서 일자리 창출 계획에 자금을 지원하여 독일 전역에 공공사업을 건설하고 있습니다. ?]

[? if unemployed >= 10 : 부르주아들은 이 계획을 독일 경제를 구하기 위해 필요한 '차악’의 대안으로 보고 승인합니다. ?]

[? if unemployed < 10 : 실업률이 충분히 낮아서 이 계획이 의도했던 효과를 거두지 못하는 것 같습니다. ?]

[? if in_popular_front or in_left_front: 공산당은 이 경제 계획을 진정한 사회주의로 나아가는 길이 아닌 자본주의를 구제하는 것으로 보고 반대합니다. ?]

@moderate_plan_limited_budget
title: 개혁파의 일자리 창출 계획을 더 낮은 비용으로 부분적으로 실행한다.
subtitle: 예산 -1.
unavailable-subtitle: 예산 -1. [? if budget < 1 : 필요한 예산이 없습니다. ?] [? if unemployed < 8 : 실업률이 충분히 낮아 일자리 창출이 필요하지 않습니다. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 1 and unemployed >= 8
on-arrival: budget -= 1; works_program += 1; unemployed -= 2 if unemployed >= 8; inflation += 1; workers_spd += 4*(1-dissent); unemployed_spd += 2*(1-dissent); new_middle_spd += 2*(1-dissent); old_middle_spd += 1*(1-dissent); rural_spd += 1*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; economic_growth += 0.7; pro_republic += 3*(1-dissent);
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : 일자리 창출 계획을 이행하기 시작했습니다! 독일 전역에 공공사업 프로젝트를 건설하기 위한 자금이 투입되어 실업률을 낮추고 있습니다. ?]

[? if moderate_plan_progress > 1 : 우리는 계속해서 일자리 창출 계획에 자금을 지원하여 독일 전역에 공공사업을 건설하고 있습니다. ?]

[? if unemployed >= 10 : 부르주아들은 이 계획을 독일 경제를 구하기 위해 필요한 '차악’의 대안으로 보고 승인합니다. ?]

[? if unemployed < 10 : 실업률이 충분히 낮아서 이 계획이 의도했던 효과를 거두지 못하는 것 같습니다. ?]

[? if in_popular_front or in_left_front: 공산당은 이 경제 계획을 진정한 사회주의로 나아가는 길이 아닌 자본주의를 구제하는 것으로 보고 반대합니다. ?]
