title: 헌법 개정
new-page: true
is-card: true
view-if: spd_in_government = 1 and justice_minister_party = "SPD" and constitutional_reform_timer = 0 and judicial_reform >= 4 and neorevisionism and ((in_weimar_coalition) or (in_spd_majority) or (in_left_front) or (in_popular_front)) and constitutional_reform < 3
frequency: 25
on-arrival: month_actions += 1; constitutional_reform_timer += 12; pass_threshold = 0.51; pass_threshold = 0.6 if pro_republic < 65;
card-image: img/portraits/RadbruchGustav.jpg
go-to: reforms_menu
tags: govt_affairs, cabinet
#subtitle: Major changes to government.

# technically you could do some trickery to get a 2/3 quorum in the Reichstag by stopping some people from attending... if the NSDAP is boycotting the Reichstag, or if the NSDAP or DNVP are banned in the aftermath of a coup atempt...

# alternatively, a constitutional referendum can be initiated, that requires a simple majority of voters to pass (as I understand article 76). This should depend on support for the republic and for democracy, and the relationships with the other parties (more similar to the presidential election).

= 헌법 개정

# the ideas for constitutional reform here are based on the bundesrepublik basic law... I'm not sure if it's totally realistic to what someone like Mierendorff would've wanted.

많은 신수정주의자들은 바이마르 헌법이 불충분하다고 여깁니다. 그들은 헌법 조항들 자체가 국가 불안정의 한 원인이며, 더 안정적인 민주 공화국을 세우기 위해서는 반드시 개정되어야 한다고 믿습니다. 이는 사회주의자와 자유주의자 모두의 생각입니다.

헌법 개정은 제국의회 3분의 2 이상의 찬성이 있거나, 혹은 유권자 과반수의 지지를 얻는 국민투표를 통해 가능합니다. 제국의회에서 절대 다수를 확보하지 못할 경우, 국민투표를 위해 다른 정당들의 강력한 지지를 확보해야만 합니다.

@reforms_menu

- @vote_threshold: 봉쇄 조항을 변경한다.
- @constructive_vonc: 비건설적인 불신임 투표를 금지한다.
- @presidential_powers: 대통령 권한을 축소한다.
- @as_is: 헌법 개정을 추진하지 않는다.
- @easy_discard
- @cancel_advisor_action
#- @constitutional_bans: 반 헌법적 정당들의 해산을 허용한다.

@vote_threshold
subtitle: 정당이 제국의회 의석을 얻기 위한 최소 득표율을 5%로 변경한다.
on-arrival: {!
Q.reform_support = Q.spd_normalized;
if (Q.z_relation > 30) {
    Q.reform_support += Q.z_normalized - 0.03;
}
if (Q.kpd_relation > 50 && Q.kpd_normalized >= 0.1) {
    Q.reform_support += Q.kpd_normalized;
}
if (Q.nsdap_normalized >= 0.2 ) {
    Q.reform_support += Q.nsdap_normalized;
}
!}
view-if: electoral_threshold = 0

# The left is opposed to this.

몇몇 정치 사상가들은 군소 정당의 난립이 공화국 불안정의 원인이라고 생각합니다. 그들은 더 높은 봉쇄 조항이 소규모 급진 정당들의 영향력을 줄일 것이라고 믿습니다. 선거구에서 최다 득표를 한 정당은 의석을 유지하게 되는데, 이는 바이에른 인민당(BVP)과 같은 지역 정당에게 유리한 조치입니다.

군소 정당들은 당연히 이 개혁에 반대할 것입니다. [? if z_relation > 30 : 중앙당은 이 개혁을 지지하지만, 그들의 연대 파트너인 바이에른인민당은 반대합니다. ?] [? if kpd_relation > 50 and kpd_normalized >= 0.1 : 놀랍게도, 공산당은 이 개혁을 지지합니다. ?] [? if nsdap_normalized >= 0.2 : 나치당은 자신들에게 이득이 될 것이라 믿고 이 개혁을 지지합니다. ?]

사민당 내 좌파는 이 변경안이 반민주적이라며 반대합니다.

- @vote_threshold_2: 개혁안 통과를 위해 국민투표를 소집한다!
- @reforms_menu: 다른 개정안 보기.


@vote_threshold_2
choose-if: reform_support >= pass_threshold
unavailable-subtitle: 이 정책에 대한 지지가 충분하지 않습니다. [? if pro_republic < 65 : 유권자 과반수의 찬성이 필요한데, 공화국을 지지하지 않는 많은 유권자들이 참여하지 않을 것이므로 국민투표에 여유 표가 더 필요합니다. ?] [? if z_relation <= 30 : 중앙당은 이 정책을 지지할 만큼 우리를 신뢰하지 않습니다. ?]
on-arrival: constitutional_reform += 1; electoral_threshold = 5; ddp_relation -= 25; dvp_relation -= 25; left_dissent += 20; workers_dnvp += workers_other*0.4; workers_nsdap += workers_other*0.4; workers_other *= 0.1; new_middle_dvp += new_middle_other*0.3; new_middle_nsdap += new_middle_other*0.3; new_middle_other *= 0.1; old_middle_dvp += 0.3*old_middle_other; old_middle_nsdap += 0.3*old_middle_other; old_middle_dnvp += 0.3*old_middle_other; old_middle_other *= 0.1; rural_dnvp += 0.5*rural_other; rural_nsdap += 0.4*rural_other; rural_other *= 0.1; unemployed_nsdap += 0.3*unemployed_other; unemployed_kpd += 0.4*unemployed_other; unemployed_other *= 0.2; catholics_z += 0.6*catholics_other; catholics_other *= 0.1;
achievement: verfassungsreform

# this should result in a redistribution of votes - other parties should not get so many votes anymore. maybe ddp and dvp get a recovery of middle-class votes? or maybe they flow to the others.

우리는 봉쇄 조항을 5%로 올리는 헌법 개정안을 통과시켰습니다. 이는 거대 정당에 이익이 되고 군소 정당에는 불리하게 작용할 것입니다.



@constructive_vonc
subtitle: 불신임 투표는 대안 정부 구성안이 통과될 수 있을 때만 발의할 수 있게 하여, 불신임 투표의 빈도를 크게 줄인다.
on-arrival: {!
Q.reform_support = Q.spd_normalized + Q.ddp_normalized;
if (Q.z_relation >= 30) {
    Q.reform_support += Q.z_normalized - 0.03;
}
if (!Q.stresemann_dead) {
    Q.reform_support += Q.dvp_normalized;
}
!}
view-if: constructive_vonc = 0

[? if z_relation >= 30 : 바이마르 연정 파트너인 [+ ddp_name +]와 중앙당은 이 개혁을 지지하며, 이는 정부 안정성을 크게 향상시킬 것입니다. ?] [? if z_relation < 30 : [+ ddp_name +]는 이 개혁을 지지하지만, 중앙당은 국민투표를 지지할 만큼 우리를 신뢰하지 않습니다. ?] 더 보수적이고 급진적인 우익 정당들과 공산당은 공화국 체제의 불안정 자체를 즐기는 듯 이 개정안에 반대하고 있습니다.

- @constructive_vonc_2: 개정안 통과를 위해 국민투표를 소집한다!
- @reforms_menu: 다른 개정안 보기.

@constructive_vonc_2
choose-if: reform_support >= pass_threshold
unavailable-subtitle: 이 정책에 대한 지지가 충분하지 않습니다. [? if pro_republic < 65 : 유권자 과반수의 찬성이 필요한데, 공화국을 지지하지 않는 많은 유권자들이 참여하지 않을 것이므로 국민투표에 여유 표가 더 필요합니다. ?] [? if z_relation <= 30 : 중앙당은 이 정책을 지지할 만큼 우리를 신뢰하지 않습니다. ?]
on-arrival: constitutional_reform += 1; constructive_vonc = 1
achievement: verfassungsreform

우리는 새 정부 구성이 불가능할 경우 불신임 투표를 금지하는 헌법 개정안을 성공적으로 통과시켰습니다. 이로써 정부의 안정성이 높아지기를 바랍니다.

@presidential_powers
subtitle: 대통령은 더 이상 마음대로 제국의회를 해산하거나 주 정부에 대한 *국가 강제 집행(Reichsexekution)*을 명령할 수 없다.
on-arrival: {!
Q.reform_support = Q.spd_normalized + Q.ddp_normalized + Q.kpd_normalized;
if (Q.z_relation > 49 && (Q.president == "Hindenburg" || Q.president == "Eckener")) {
    Q.reform_support += Q.z_normalized;
} else if (Q.president == "Braun") {
    Q.reform_support += Q.z_normalized - 0.03;
}
!}
view-if: presidential_powers = 0

헌법이 보장하는 광범위한 대통령 권한은 권위주의로 향하는 문을 열어줄 수 있습니다. 따라서 우리는 선출된 정부를 일방적으로 해산하고 주 정부를 해임하는 대통령의 권한을 끝내는 등, 대통령의 권력을 제한하는 개혁을 지지할 수 있습니다.

공산당(KPD)은 과거에 *국가 강제 집행*의 희생자였기 때문에(KPD가 지지했던 튀링겐과 작센 주 정부가 해임되었을 때) 이 개혁에 찬성합니다. [+ ddp_name +]의 민주주의자들 또한 이를 지지합니다. [? if president == "Hindenburg": 그러나 보수파는 대통령 권한 축소를 힌덴부르크에 대한 무례로 여겨 격렬하게 반대합니다. ?] [? if z_relation <= 49 and president == "Hindenburg" : 따라서 중앙당은 이 개혁에 반대하도록 영향을 받았습니다. ?] [? if z_relation > 49 and president == "Hindenburg" : 그럼에도 불구하고 중앙당은 제안된 개혁안을 지지합니다. ?] [? if president == "Braun" : 중앙당은 이 개혁을 지지합니다. ?]

- @presidential_powers_2: 개정안 통과를 위해 국민투표를 소집한다!
- @reforms_menu: 다른 개정안 보기.

@presidential_powers_2
choose-if: reform_support >= pass_threshold
unavailable-subtitle: 이 정책에 대한 지지가 충분하지 않습니다. [? if pro_republic < 65 : 권자 과반수의 찬성이 필요한데, 공화국을 지지하지 않는 많은 유권자들이 투표에 참여하지 않을 것이므로 국민투표에 여유 표가 더 필요합니다.  ?] [? if z_relation <= 49 and president != "Braun" : 중앙당은 이 정책을 지지할 만큼 우리를 신뢰하지 않습니다. ?]
on-arrival: constitutional_reform += 1; presidential_powers = 1
achievement: verfassungsreform

우리는 대통령 권한을 제한하는 헌법 개정안을 성공적으로 통과시켰습니다! 이로써 독일이 덜 권위주의적인 국가가 되기를 바랍니다.



@constitutional_bans

# Temporarily disable this for now - how would this interact with the already-extant law for the protection of the republic? This 


@as_is
go-to: root
