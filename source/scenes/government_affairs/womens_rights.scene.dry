title: 여성권
new-page: true
is-card: true
tags: govt_affairs, cabinet
view-if: spd_in_government = 1 and womens_rights_timer = 0 and chancellor_party == "SPD"
on-arrival: womens_rights_timer += 10; month_actions += 1
card-image: img/portraits/PfülfAntonie.jpg
frequency: 50
max-visits: 6

= 여성권

헌법에 따르면 여성은 남성과 동등하지만, 현실은 그렇지 않습니다. 노동 현장의 여성들은 동일 노동에 대해 동등한 임금을 받지 못할 뿐만 아니라, 결혼 후 종종 직장에서 밀려납니다. 남성은 여전히 결혼 및 가족 정책에서 우대받으며, 다른 많은 영역에서 여성의 권리가 제한됩니다. 독일 사회에 만연한 압도적인 가부장적 문화는 말할 것도 없습니다.

좋든 나쁘든, 사회주의자들은 여성 인권 신장을 지지하는 데 있어 거의 유일한 세력이며, 심지어 당내에서조차 지도부는 이를 우선순위로 여기지 않습니다. [? if spd_r < 50 : 안타깝게도, 여성들은 여전히 부르주아 정당을 더 많이 지지하는 경향이 있습니다. ?]

- @labor: 노동 현장의 여성을 지원하는 정책을 수립한다.
- @family: 혼인, 이혼 및 재산 관련 법을 개정한다.
- @welfare: 한부모 가정과 아동을 위한 복지를 개선한다.
- @liberalize: 낙태법 자유화를 시도한다.
- @return: 지금은 어떤 정책도 시행하지 않는다.
- @easy_discard
- @cancel_advisor_action

@labor
subtitle: 유급 출산 휴가, 동일 임금 정책 등.
on-arrival: womens_work += 1; womens_rights += 1; workers_spd -= 4 if unemployed > 15; new_middle_spd += 4*(1-dissent); workers_spd += 4*(1 - dissent); economic_growth += 0.1;
max-visits: 3

이 정책들 덕분에 집 밖에서 일하는 '신여성’들 사이에서 우리 당의 인기가 높아지고 있습니다. [? if unemployed > 15 : 하지만, 특히 실업률이 상승하는 상황에서 많은 남성 노동자들이 직장 내 여성들에 대해 반발하고 있습니다. ?]

@family
subtitle: 여성을 더 동등하게 대우하도록 가족법과 상속법을 개정한다.
on-arrival: family_law += 1; womens_rights += 1; new_middle_spd += 2*(1-dissent); workers_spd += 3*(1-dissent); old_middle_spd -= 2; rural_spd -= 2; catholics_spd -= 4; coalition_dissent += 1; z_relation -= 4;
max-visits: 2

이 정책들은 여성이 결혼 생활 내에서 이혼할 권리를 포함한 동등한 권리를 보장하는 것을 목표로 합니다. 사회의 보수적인 계층은 우리의 개혁이 사회 구조에 해악을 끼친다고 여깁니다.


@welfare
subtitle: -1 budget
choose-if: budget > 0
on-arrival: welfare += 1; workers_spd += 5*(1-dissent); unemployed_spd += 5*(1-dissent); budget -= 1; womens_rights += 1; coalition_dissent += 1 if welfare >= 1 and unemployed >= 15; unemployed -= 0.4; inflation += 0.2; economic_growth += 0.3;

우리는 여성과 아동에 대한 복지 지출을 늘렸습니다. [? if welfare >= 1 and unemployed >= 15 : 일부 연정 파트너들은 현 시점에서 복지 지출 증액을 낭비로 여기고 있습니다. ?]


@liberalize
max-visits: 1
subtitle: [? if progressive_coalition < 50 : 성공할 가능성이 낮습니다. 과반의 지지를 얻지 못했습니다. ?]
go-to: liberalize_fail if progressive_coalition < 50; liberalize_success if progressive_coalition >= 50

@liberalize_fail
on-arrival: z_relation -= 5; coalition_dissent += 1; workers_spd += 3*(1-dissent); catholics_spd -= 5;

안타깝게도, 우리의 제안은 제국의회에서 부결되었습니다. 우리의 오랜 파트너인 가톨릭 중앙당이 특히 격렬하게 반대했고, 우리 당원 일부도 당론을 어겼습니다. 이 시도에 좌절한 활동가들은 때를 기다리기로 했습니다.

@liberalize_success
on-arrival: abortion_rights += 1; womens_rights += 1; coalition_dissent += 1; z_relation -= 8; catholics_spd -= 10; dvp_relation -= 5; workers_spd += 3*(1-dissent); new_middle_spd += 3*(1-dissent); workers_z += 3; new_middle_z += 3; old_middle_z += 3; old_middle_dnvp += 3; rural_dnvp += 3; workers_nsdap -= 3; new_middle_nsdap -= 3; old_middle_nsdap -= 2; rural_nsdap -= 2; unemployed_nsdap -= 2; unemployed_z += 3; unemployed_spd += 3*(1-dissent)

공산당과 [+ ddp_name +]을 포함한 '진보 연정’의 지지로, 우리는 독일에서 낙태를 합법화하는 법안을 성공적으로 통과시켰습니다! 이는 우리의 더 진보적인 지지자들에게 힘을 실어주었지만, 중앙당과의 관계에는 악영향을 미쳤습니다. 낙태에 대한 정치적 논의가 활발해지면서, 종교적 성향의 중앙당과 독일국민당은 나치당의 표를 빼앗아 보수표를 결집했습니다.
