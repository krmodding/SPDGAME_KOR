title: 정적 조사
on-arrival: month_actions += 1
view-if: spd_in_government = 1 and interior_minister_party = "SPD"
#card-image: img/portraits/SeveringCarl.jpg
#is-card: true
#new-page: true

# this is not a card...

= 국내의 적 수사

내무부를 장악한 지금, 우리는 경찰과 정보 기관을 지휘하여 국내의 정적들을 수사할 수 있습니다. 다양한 조직들의 활동을 금지할 수도 있습니다.

- @deport_hitler
- @ban_sa: 돌격대를 금지한다.
- @unban_sa: 돌격대 금지를 해제한다.
- @persecute_sa
- @ban_sh: 철모단을 금지한다.
- @unban_sh: 철모단 금지를 해제한다
- @persecute_sh
- @ban_rfb: 적색전선전사동맹을 금지한다.
- @unban_rfb: 적색전선전사동맹 금지를 해제한다.
- @persecute_rfb
- @root: 지금은 아무 조치도 취하지 않는다.

@ban_sa
view-if: nazi_urgency >= 3 and sa_banned == 0
on-arrival: sa_strength *= 0.85; sa_strength *= 0.7 if interior_police_loyalty >= 0.7; sa_banned = 1; dvp_relation -= 3; prussian_police_loyalty -= 0.05; interior_police_loyalty -= 0.05; sa_militancy -= 0.05; sa_ban_timer = 6;

알고 보니 나치 돌격대(SA)는 경찰과 관료 조직에 많은 협력자를 두고 있었습니다. 경찰과의 관계는 악화되고, SA는 여전히 경찰의 눈앞에서 불법적인 조직 활동을 계속할 수 있습니다. [? if interior_police_loyalty >= 0.7 : 그럼에도 불구하고, SA는 우리의 금지 조치로 인해 상당히 약화됩니다. ?]

@unban_sa
view-if: sa_banned = 1
on-arrival: sa_banned = 0; sa_strength += 100; sa_militancy += 0.05

돌격대 금지가 해제되었습니다. 그들은 즉시 대대적인 조직원 모집에 나섭니다.

@persecute_sa
title: 돌격대에 대한 탄압을 강화한다.
view-if: sa_banned = 1
choose-if: judicial_reform >= 3 and interior_police_loyalty >= 0.7
unavailable-subtitle: [? if judicial_reform < 3 : 사법부가 이를 허용하지 않을 것입니다. ?] [? if interior_police_loyalty < 0.7 : 경찰의 충성도가 너무 낮아 이 임무를 맡기기엔 신뢰할 수 없습니다. ?]
on-arrival: sa_strength *= 0.7; sa_militancy -= 0.05

우리는 돌격대 지도자들을 추가로 체포하고 그들의 무기를 압수했습니다.

@ban_sh
on-arrival: sh_strength *= 0.85; sh_strength *= 0.7 if interior_police_loyalty >= 0.7; sh_banned = 1; dvp_relation -= 5; prussian_police_loyalty -= 0.1; interior_police_loyalty -= 0.05; coalition_dissent += 1 if spd_in_government = 1 and in_grand_coalition = 1 and president = "Hindenburg"; sh_ban_timer = 6;
view-if: sh_banned == 0

[? if president == "Hindenburg": 철모단을 금지하면 힌덴부르크 대통령뿐만 아니라 우익 정치권 및 경찰과의 관계가 악화됩니다. ?][? if president != "Hindenburg": 철모단을 금지하면 우익 정치권 및 경찰과의 관계가 악화됩니다. ?] [? if spd_in_government = 1 and in_grand_coalition = 1 and president = "Hindenburg" : 또한 힌덴부르크가 우리 연정 내 우파에 미치는 영향력 때문에 우리 연정 또한 약화됩니다. ?] 게다가, 철모단은 여전히 은밀하게 활동하고 있습니다.

@unban_sh
view-if: sh_banned = 1
on-arrival: sh_banned = 0; sh_strength += 100

철모단 금지가 해제되었습니다.

@persecute_sh
title: 철모단에 대한 탄압을 강화한다.
view-if: sh_banned = 1
choose-if: judicial_reform >= 4 and interior_police_loyalty >= 0.7
unavailable-subtitle: [? if judicial_reform < 4 : 사법부가 이를 허용하지 않을 것입니다. ?] [? if interior_police_loyalty < 0.7 : 경찰의 충성도가 너무 낮아 이 임무를 맡기기엔 신뢰할 수 없습니다. ?]
on-arrival: sh_strength *= 0.7; sh_militancy -= 0.05; dvp_relation -= 5; prussian_police_loyalty -= 0.1; interior_police_loyalty -= 0.05; coalition_dissent += 1 if spd_in_government = 1 and in_grand_coalition = 1 and president = "Hindenburg";

우리는 철모단의 지도자들을 추가로 체포하고 그들의 무기를 압수했습니다.


@ban_rfb
view-if: communist_coalition < 3 and rfb_banned = 0
on-arrival: kpd_relation -= 15; rfb_strength -= 50; rfb_banned = 1; communist_coalition -= 1; z_relation += 5; dvp_relation += 5; prussian_police_loyalty += 0.1; interior_police_loyalty += 0.1; kpd_coalition_dissent += 2; coalition_dissent -= 1 if coalition_dissent > 0 and n_rfb_banned = 0; n_rfb_banned += 1

경찰은 이 금지령을 열성적으로, 어쩌면 지나치게 열성적으로 집행합니다. 공산주의자들은 이를 두고 사회주의자들이 좌파를 배신했다는 또 하나의 증거로 삼습니다. 하지만 온건파와 중도우파 정당들, 그리고 경찰은 이 조치를 지지합니다.

@unban_rfb
view-if: rfb_banned = 1
on-arrival: rfb_banned = 0; kpd_relation += 5; rfb_strength += 50; prussian_police_loyalty -= 0.05; interior_police_loyalty -= 0.05

공산주의 적색전선전사동맹(RFB) 금지가 해제되었습니다.

@persecute_rfb
title: Further persecute the RFB.
view-if: rfb_banned = 1
on-arrival: rfb_strength *= 0.7; rfb_militancy -= 0.05; kpd_relation -= 10; kpd_coalition_dissent += 1; z_relation += 5; dvp_relation += 5; prussian_police_loyalty += 0.1; interior_police_loyalty += 0.1; coalition_dissent -= 1 if coalition_dissent > 0 and n_rfb_persecuted = 0; n_rfb_persecuted += 1

우리는 적색전선전사동맹(RFB)의 지도자들을 추가로 체포하고 그들의 무기를 압수했습니다.
