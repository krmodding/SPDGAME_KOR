title: 언론
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg
#subtitle: Setting the direction for our newspapers and radio.

= 언론 매체

사민당은 독일의 모든 도시와 지역에 수많은 신문을 보유한, 실로 거대한 언론 제국을 운영하고 있습니다. 이 언론을 다양한 목적으로 활용할 수 있을 것입니다…

- @commercial
- @campaign
- @ideological
- @radio
- @radio_2
- @radio_3
- @radio_4
- @return: 지금은 언론 관련 활동을 하지 않는다.
- @easy_discard
- @cancel_advisor_action

@commercial
title: 당 지지층 너머로 독자층을 넓혀 우리 신문의 상업성을 강화한다.
subtitle: 이념적 순수주의자들은 불만을 품겠지만, 더 많은 자금을 확보하고 중산층에게 사회주의적 시각을 접하게 할 기회가 될 수 있습니다.
on-arrival: resources += 1; commercialized_media += 1; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1-dissent); left_dissent += 10; labor_dissent += 10

상업화된 신문 덕분에 더 많은 자금을 확보할 수 있게 되었습니다. 하지만, 좌파와 노동계 분파는 불만을 표합니다.

@campaign
title: 우리 언론의 목표는 더 많은 사민당원을 당선시키는 것이다.
subtitle: 당보들은 선거 운동에 집중할 것입니다.
on-arrival: campaign_media += 1; workers_spd += 4*(1 - dissent); unemployed_spd += 2*(1-dissent);

우리의 선전 매체는 이미 사회주의 선전으로 포화된 노동자들에게만 주로 전달되고 있지만, 그래도 무언가 효과가 있기를 바랄 뿐입니다.

@ideological
title: 신문 지면을 통해 활발한 이념 토론을 벌인다.
subtitle: 모든 분파가 자신의 견해를 자유롭게 펼칠 수 있는 공간이 마련될 것입니다.
on-arrival: left_dissent -= 6; labor_dissent -= 6; center_dissent -= 6; reformist_dissent -= 6; neorevisionist_dissent -= 6

이것이 역사적으로 우리 당 언론의 노선이었으며, 당내의 수많은 갈등을 성공적으로 봉합해 왔습니다.

@radio
title: 라디오 방송국을 설립하는 것은 어떨까?
subtitle: 자금 -2
choose-if: resources >= 2
unavailable-subtitle: 자금이 부족합니다.
view-if: radio = 0
on-arrival: radio += 1; resources -= 2; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 2*(1 - dissent); labor_dissent += 5; socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent) if pacifism > 1; pro_republic += 3*(1-dissent) if democratization > 1

새로운 라디오 방송국 덕분에, 우리는 이전보다 더 친밀한 환경에서 사람들에게 다가갈 수 있게 되었습니다. 음악과 다른 대중적인 콘텐츠와 함께 사회주의 메시지를 전파하는 것입니다. 지금 당장 대부분의 노동자들은 가정용 라디오를 살 여유가 없어서, 우리의 방송은 주로 중산층에게 전달되고 있으며, 노동계 동지들은 이 사실을 뼈아프게 인식하고 있습니다.

@radio_2
title: 계속해서 라디오 방송망을 확장해야 한다.
subtitle: 자금 -1
choose-if: resources >= 1
unavailable-subtitle: 자금이 부족합니다.
view-if: radio > 0 and radio <= 3
on-arrival: radio += 1; resources -= 1; new_middle_spd += 5*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 3*(1 - dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

우리의 새로운 라디오 방송망은 대중적인 프로그램과 정치적인 프로그램을 혼합하여 성공을 거두고 있습니다.

@radio_3
title: 계속해서 라디오 방송망을 확장해야 한다.
subtitle: 이제 재정적으로 자립하여 더 이상 자금이 필요하지 않습니다.
view-if: radio > 3 and radio <= 5 and year <= 1931
on-arrival: radio += 1; new_middle_spd += 5*(1 - dissent); old_middle_spd += 4*(1 - dissent); workers_spd += 4*(1 - dissent); rural_spd += 3*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

우리의 라디오 방송망은 우리의 메시지를 전파하는 성공적인 수단이 되었습니다.

@radio_4
title: 라디오 방송망을 계속해서 지원해야 한다.
subtitle: 라디오가 보편화되면서 참신함이 사라지고 있으며, 사회는 라디오 방송으로 포화 상태가 되어가고 있습니다. 자금 -1
choose-if: resources >= 1
unavailable-subtitle: 자금이 부족합니다.
view-if: radio > 3 and (radio >= 6 or year >= 1932) and radio <= 9
on-arrival: radio += 1; resources -= 1; new_middle_spd += 3*(1 - dissent); old_middle_spd += 2*(1 - dissent); workers_spd += 3*(1 - dissent); rural_spd += 2*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 2*(1 - dissent); nationalism -= 2*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 2*(1-dissent)*(democratization-2) if democratization > 2

우리는 계속해서 라디오 방송국에 투자하고 있습니다. 상업 방송과 정치 방송 모두가 점점 더 널리 퍼지면서, 경쟁 속에서 우리의 메시지를 전파하기가 더욱 어려워지고 있습니다.
